apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def siteUrl = 'https://github.com/wyouflf/xUtils3'
def gitUrl = 'https://github.com/wyouflf/xUtils3.git'
group = "org.xutils"
version = "3.3.32"

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 20160406
        versionName version
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            java.srcDirs = ['src/main/java', 'src/main/java_compat']
        }
    }

    afterEvaluate {
        tasks.matching {
            it.name.startsWith('dex')
        }.each { dx ->
            if (dx.additionalParameters == null) {
                dx.additionalParameters = []
            }
            //enable multidex
            dx.additionalParameters += '--multi-dex'

        }
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.android.support:support-v4:23.1.0'
    compile 'com.android.support:recyclerview-v7:23.1.0'
    compile 'com.android.support:design:23.2.0'

    compile 'com.orhanobut:logger:1.3'
    compile 'com.alipay.euler:andfix:0.4.0@aar'
    compile 'com.android.support:multidex:1.0.1'

    //https://github.com/greenrobot/EventBus
    compile 'org.greenrobot:eventbus:3.0.0'

    //https://github.com/JakeWharton/butterknife
    compile 'com.jakewharton:butterknife:7.0.1'

    //文本输入框，验证功能
    //https://github.com/inmite/android-validation-komensky
    compile 'eu.inmite.android.lib:android-validation-komensky:0.9.4@aar'

    //<style name="AppTheme" parent="Theme.AppCompat.Light.DarkActionBar">
    //<item name="colorPrimary">@color/indigo</item>
    //<item name="colorPrimaryDark">@color/indigo_dark</item>
    //<item name="colorAccent">@color/pink</item>
    //</style>
    //https://github.com/avast/android-styled-dialogs
    compile 'com.avast:android-styled-dialogs:2.3.3'
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name 'xUtils'
                description 'android orm, bitmap, http, view inject... '
                // #CONFIG# // project title
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'wyouflf'
                        // #CONFIG# // your user id (you can write your nickname)
                        name 'lei.jiao'
                        // #CONFIG# // your user name
                        email 'wyouflf@qq.com'
                        // #CONFIG# // your email
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    options.encoding = "UTF-8"
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "xUtils"
        // #CONFIG# project name in jcenter
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}